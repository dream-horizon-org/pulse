buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['PulseReactNativeOtel_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["PulseReactNativeOtel_" + name]).toInteger()
}

android {
  namespace "com.pulsereactnativeotel"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
      consumerProguardFiles 'proguard-rules.pro'
    }
    debug {
      consumerProguardFiles 'proguard-rules.pro'
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation("com.facebook.react:react-android")
  implementation("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")

  api("org.dreamhorizon:pulse-android-sdk:0.0.1-alpha") {
    exclude group: "com.squareup.okhttp3", module: "okhttp-jvm"
  }
  api("com.squareup.okhttp3:okhttp:5.0.0-alpha.14")
}

configurations.all {
  resolutionStrategy {
    eachDependency {
      if (requested.group == "com.squareup.okhttp3" && requested.name == "okhttp-jvm") {
        useTarget("com.squareup.okhttp3:okhttp:${requested.version}")
        because("Resolving okhttp-jvm to okhttp for Android compatibility")
      }
    }
  }
}
